plugins {
	id 'java'
	id 'maven-publish'
	id 'jacoco'
    id "com.jfrog.bintray" version "1.8.5"
}

repositories {
    mavenCentral()
    jcenter()
}

ext{
	vertxVersion = '4.0.0'
	vertxElasticsearchIndexer = '1.1.0'
	jupiterVersion = '5.7.0'
	jupiterLauncherVersion ='1.7.0'
	slfApiVersion = '1.7.30'
	logbackVersion = '1.2.3'
}

dependencies {

	implementation "io.vertx:vertx-core:${vertxVersion}"
	implementation "org.slf4j:slf4j-api:${slfApiVersion}"
	implementation "com.mdac:vertx-elasticsearch-indexer:${vertxElasticsearchIndexer}"
    implementation "ch.qos.logback:logback-classic:${logbackVersion}"
	
	testImplementation "io.vertx:vertx-web:${vertxVersion}"
	testImplementation "io.vertx:vertx-config:${vertxVersion}"
	testImplementation "io.vertx:vertx-config-yaml:${vertxVersion}"
	
	testImplementation "org.junit.jupiter:junit-jupiter-api:${jupiterVersion}"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${jupiterVersion}"
    testImplementation "org.junit.platform:junit-platform-launcher:${jupiterLauncherVersion}"
	
}


sourceCompatibility='1.8'
targetCompatibility='1.8'

jar.archiveFileName = "vertx-logback-elasticsearch-appender-1.2.0.jar"
bintrayUpload.packageName = "vertx-logback-elasticsearch-appender-1.2.0.jar"

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            
            groupId 'com.mdac'
            artifactId 'vertx-logback-elasticsearch-appender'
            version '1.2.0'
            
            from components.java

            artifact sourceJar {
                classifier "sources"
            }
        }
    }
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

bintray {
	user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
	key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    
    publications = ['mavenJava']
    dryRun = false
    publish = true
    pkg {
        repo = 'maven'
        name = 'vertx-logback-elasticsearch-appender'
        licenses = ['Apache-2.0']
        labels = ['vertx', 'log', 'logback', 'elasticsearch']
        publicDownloadNumbers = true
        //Optional version descriptor
        override = false
        vcsUrl = 'https://github.com/romanpierson/vertx-logback-elasticsearch-appender.git'
        version {
        	name = '1.2.0'
        }
    }
}

jacocoTestReport {
	reports {
		xml.enabled = true
		html.enabled = true
	}
}

wrapper() {
  gradleVersion = '6.7.1'
}

